/*
 * Copyright (c) 2021, Alden Torres
 *
 * Licensed under the terms of the MIT license.
 * Copy of the license at https://opensource.org/licenses/MIT
 */

plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
}

group = "org.ssohub"
version = "1.0.6"

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.2"
}

test {
    useJUnitPlatform()
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standard_out"

        exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
        showExceptions = true
        showCauses = true
        showStackTraces = true
    }

    systemProperty("java.library.path", file(".").absolutePath)
    environment("LD_LIBRARY_PATH", file(".").absolutePath)
}

jar {

    from("../build") {
        include("libecc-jvm.dylib")
        into("lib")
    }

    from("../build") {
        include("libecc-jvm.so")
        into("lib")
    }
}

if (!hasProperty('ossrhUsername')) {
    ext.ossrhUsername = ''
}

if (!hasProperty('ossrhPassword')) {
    ext.ossrhPassword = ''
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "ecc"
                description = "elliptic-curve cryptography"
                url = "https://github.com/aldenml/ecc"
                scm {
                    connection = "scm:git:git://github.com/aldenml/ecc.git"
                    developerConnection = "scm:git:ssh:git@github.com/aldenml/ecc.git"
                    url = "https://github.com/aldenml/ecc"
                }
                licenses {
                    license {
                        name = "The MIT License"
                        url = "https://github.com/aldenml/ecc/blob/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "aldenml"
                        name = "Alden Torres"
                        email = "aldenml@gmail.com"
                    }
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
            url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
